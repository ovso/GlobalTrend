apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

apply from: '../scripts/ktlint.gradle'

def keystorePropertiesFile = rootProject.file("../jks/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
android {

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion config.compile_sdk
    defaultConfig {
        applicationId config.app_id
        minSdkVersion config.min_sdk
        targetSdkVersion config.target_sdk
        versionCode config.version_major * 1000000 + config.version_minor * 1000 + config.version_patch
        versionName "${config.version_major}.${config.version_minor}.${config.version_patch}"
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "GlobalTrend_${versionCode}_$versionName")
    }
    buildTypes {
        debug {
            minifyEnabled false
            resValue("string", "ads_app_id", "ca-app-pub-3940256099942544~3347511713")
            resValue("string", "ads_banner_unit_id", "ca-app-pub-3940256099942544/6300978111")
            resValue("string", "ads_interstitial_id", "ca-app-pub-3940256099942544/1033173712")
        }
        release {

            minifyEnabled true
            shrinkResources true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-rules.pro'
            proguardFile 'proguard-firebase.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            resValue("string", "ads_app_id", "ca-app-pub-8679189423397017~8521853317")
            resValue("string", "ads_banner_unit_id", "ca-app-pub-8679189423397017/1841551438")
            resValue("string", "ads_interstitial_id", "ca-app-pub-8679189423397017/9803145397")
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    def formattedDate = new Date().format('yyyyMMddHHmmss')
                    def appName = variant.getApplicationId().split(/[.]/)[3]
                    outputFileName =
                            "${appName}-${variant.name}-${variant.versionCode}-${variant.versionName}-${formattedDate}.apk"
                }
            }
        }

    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    androidExtensions {
        experimental = true
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlin
    implementation deps.androidx.values()

    implementation deps.jetbrains.kotlin_reflect

    implementation deps.firebase.ads

    implementation deps.rx.values()

    implementation deps.coroutines.values()

    implementation "com.google.dagger:dagger:2.33"
    implementation "com.google.dagger:hilt-android:2.33-beta"
    implementation "com.google.dagger:hilt-android-testing:2.33-beta"
    implementation "androidx.hilt:hilt-common:1.0.0-beta01"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt "androidx.hilt:hilt-compiler:1.0.0-beta01"
    kapt "com.google.dagger:dagger-compiler:2.33"
    kapt "com.google.dagger:hilt-android-compiler:2.33-beta"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation group: 'com.prof.rssparser', name: 'rssparser', version: '3.1.3'

    implementation 'com.orhanobut:logger:2.2.0'

    implementation 'com.google.android:flexbox:2.0.1'

    implementation deps.jsoup
    implementation deps.easydeviceinfo
    implementation deps.licensedialog
    implementation deps.timber
    implementation deps.lottie

    implementation deps.glide.runtime
    kapt deps.glide.compiler
    implementation("io.coil-kt:coil:1.2.1")

    implementation deps.retrofit.values()
    implementation deps.okhttp.values()

    testImplementation deps.atsl.junit
    androidTestImplementation deps.atsl.values()
}

kapt {
    correctErrorTypes true
}

apply plugin: 'com.google.gms.google-services'
